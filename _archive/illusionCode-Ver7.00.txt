' {$STAMP BS2}
' {$PBASIC 2.5}
'**** COMPILER DIRECTIVES********************************************************

  #DEFINE ProtoMode = 0   'Set to 1 for the proto board
                          'Set to zero for the real Illusion bd
                          'To reduce code size, automatic board detection has been eliminated
  #DEFINE No7 = 0         'To eliminate flashing of light 7 set to 1
                          'otherwise set to zero.
  #DEFINE No8 = 1         'To eliminate flashing of light 8 set to 1
                          'otherwise set to zero. This light is presently not wired correctly
  #DEFINE No9 = 1         'To eliminate flashing of light 9 set to 1
                          'otherwise set to zero.
'************************************************************************************

'*************************************************************************************
'
'         Project: Potential Replacement Simulation Program for the
'                  Empire Mine State Historic Park's Illusion Feature
'
' The Park's Illusion feature is controlled by a microcontroller model BS2
' marketed by Parallax, INC.  The existing controller with custom designed
' PBasic program does an admirable job of simulating a "man skip" progressing
' down the inclined mineshaft of the Empire Mine.
'
' I was so intrigued by the existing hardware and the rather significant cost
' to buy the rights to the existing program, that I challenged myself to code
' a program to replicate the responses generated by the original program.
'
' This version of the program is based on reproducing the timing between actions
' based on video measurements of the events in the original program. This route
' was chosen based on Empire Mine staff representatives satisfaction with the
' initial program even though the simulated speed of the manskip is several time
' the historically recorded speed to 10 feet-per-second.
'
' Should it become desirable to test this program when completed using the
' hardware of the existing Illusion controller, this program can be
' downloaded to a Parallax BS2 microcontroller and substituted for the
' existing microcontroller.
'
'         Jim Ward
'         530.273.3671
'         drawj@suddenlink.net
'
'
'
'**** CODE DEVELOPMENT HISTORY *****************************************************
'
'11-22-2012  See version 5.03 for full earlier history
'            Deleted explosions from background sounds and added Orlo presentation if the start
'            switch is pressed when the unit is reset.
'11-23-2012  Edited Orlo's presentation removing 3 minutes
'6-12-2013   Revised code to repeat Orlo indefinitely if started Rev 6.02'
'8-20-2013   Removed quiet mode when button is first pressed.  Illusion now imediately starts
'5-28-2015   Added a service mode during Orlo's presentation and corrected "9" on proto board
'
'**** DEFINITIONS *************************************************************

  ' Inter-strobe time is the time between the strobe lights used to
  ' simulate the motion of the manskip.

  ' Inter-Block-time is the time between the last light of one strobe
  ' sequence and the first light of the following light sequence as the man skip
  ' progress along the mineshaft.

'**** ILLUSION CONSTANTS ****************************************************************
  'Sound            PIN 0   'Controls the sound transcription device -- active low
                           'No longer used
  Light_1  CON %0000000010 'The pins connected to the set of lights in mine shaft
  Light_2  CON %0000000100
  Light_3  CON %0000001000
  Light_4  CON %0000010000
  Light_5  CON %0000100000
  Light_6  CON %0001000000
  Light_7  CON %0010000000
  Light_8  CON %0100000000 'This is the light directly above the button

  Light_9  CON %1000000000 'This is also a normal gallery light.
  Pressed  CON  0          'Switch states
  Released CON  1
  Relay_Vibrater   PIN 10  'Operates a relay that controls power to the vibrator
                           ' located beneath the manskip
  Relay_RoomLight  PIN 11  'Transfer control of the room lights to this program.
                           ' and turns off the sodium vapor lights in the mine
                           ' shaft.
  'RailClicker      PIN 12  'The "Clicker" simulates the sound of steel wheels
                           ' crossing the splice space between rail segments.
                           'The RailClicker is not used in the Illusion
  Bell_Sounder     PIN 13  'Sounds the bell in the gallery
  'Spare            PIN 14  'Currently no solid state relay is mounted in this
                           ' position.
  StartSwitch      PIN 15  'The input pin is connected to the docent's "start"
                           ' switch. It is active-low
  '******************Frank added CONSTANTS for Proto Board ******************
  Freq1Bell  CON   3000     'frequency 1 of bell
  Freq2Bell  CON   4000     'frequency 2 of bell
  Bell_Sounder2    PIN 9   'Pizzo Speaker on proto Board, controls a light
                           'in the Illusion
'*************** Initial variables********
  Index1           VAR Byte      'Temporary storage of For-Next Index variables
  Index2           VAR Byte      'Temporary storage of For-Next Index variables
  InterblockTime   VAR Word      'The delay between passes through the strobe
                                 ' light sequence in milliseconds
  InterlightTime   VAR Word      'The time each strobe light is energized in
                                 ' milliseconds
  MinuteCount      VAR  Word     'Variable used to count loops which determine
                                 ' when one minute has passed waiting for the
                                 ' start button to be pressed
  Flag3            VAR  Bit      'Variable to determine if going back up
  UpDown           VAR  Bit      'Variabale used to determine if going up or down
                                 '1 is going down, 0 is going up

'**** MAIN PROGRAM *****************************************************************

Main:
    'Set the pin I/O directions and Initialize

      OUTS = %0000000000000000       'Initialize ports to off default is 0 anyway

      DIRS = %0111111111111111       'The only input pin is bit 15.  7FFF hex

      PAUSE 250 'Give the sound card time to initialize

          IF (StartSwitch = Pressed )   THEN 'Just play Orlos presentation if StartSwitch
                                         'is pressed during reset

           LOW Relay_RoomLight  'Turn on the mine-shaft lights and sodium vapor lights.

           GOSUB all            'Turn on all lights in the shaft
           DO

                   #IF (ProtoMode = 0) #THEN
                        SEROUT 0,$8054,["S"]
                   #ELSE
                        SEROUT 16,$4054,["Stop "]  'Turn off all sound files
                   #ENDIF

                   PAUSE 2000

                   #IF (ProtoMode = 0) #THEN
                        SEROUT 0,$8054,["3"]  'Start Olro Presentation in the mine
                   #ELSE
                        SEROUT 16,$4054,["Orlo is speaking "]
                   #ENDIF


               PAUSE 3000

               ServiceLoop:

                IF (StartSwitch = Released ) THEN
                PAUSE 100
                GOTO ServiceLoop  'Watch for start switch being pressed
                ENDIF
                PAUSE 5000
                IF (StartSwitch = Released  ) THEN
                GOTO ServiceLoop  'Start service mode if switch pressed for 5+ seconds
                ENDIF
                   DO
                      #IF (ProtoMode = 0) #THEN
                        SEROUT 0,$8054,["S"]
                      #ELSE
                        SEROUT 16,$4054,["Stop "]  'Turn off all sound files
                      #ENDIF
                      GOSUB all
                      LOW Relay_RoomLight  'Turn on the mine-shaft lights and sodium vapor lights.
                      HIGH Relay_Vibrater  'Start the Man Skip vibrator
                      GOSUB ServiceWait
                      LOW Relay_Vibrater   'Turn off vibrator under man skip
                      GOSUB ServiceWait
                      HIGH Relay_RoomLight 'Turn off room lights
                      GOSUB one   'Turn lights on one at a time then wait for button press
                      GOSUB ServiceWait
                      GOSUB two
                      GOSUB ServiceWait
                      GOSUB three
                      GOSUB ServiceWait
                      GOSUB four
                      GOSUB ServiceWait
                      GOSUB five
                      GOSUB ServiceWait
                      GOSUB six
                      GOSUB ServiceWait
                      GOSUB seven
                      GOSUB ServiceWait
                      GOSUB eight
                      GOSUB ServiceWait
                      GOSUB nine
                      GOSUB ServiceWait
                      GOSUB all
                      GOSUB ServiceWait
                   LOOP
           LOOP
      ENDIF

      'If we get past here, the StartStop switch was not pressed during the last reset

      #IF (ProtoMode = 0) #THEN
          SEROUT 0,$8054,["S"]  'STOP all sound
      #ELSE
          SEROUT 16,$4054,["STOP "]  'STOP all sound
      #ENDIF
      PAUSE 1000

        'Start periodic processing.
      DO
            GOSUB StrtState
            GOSUB AccelState
            UpDown = 1
            GOSUB ConstSpeedState
            GOSUB DecelState
            GOSUB StpState
            IF Flag3 = 0 THEN continue5 'The Illusion is not going back up if Flag3 = 0
                                        'Flag3 is set in the previous StpState by how long
                                        'the Start Button is depressed.
            GOSUB  UpStart
            UpDown = 0
            GOSUB  ConstSpeedState
            GOSUB  UpDecel
            continue5:
            GOSUB eight         'Turn on lights slowly
            PAUSE 1000
            GOSUB all
            PAUSE 1000
      LOOP            'End of the program loop
  END                 'End of the Main program
'************************************************************************************


'********START STATE ***************************************************************

  StrtState:

      'Set the initial state.

      LOW Relay_RoomLight  'Turn on the mine-shaft lights and sodium vapor lights.

      GOSUB all            'Turn on all lights in the shaft

      #IF (ProtoMode = 0) #THEN
          SEROUT 0,$8054,["2"]  'Turns on general mining sounds.
      #ELSE
          SEROUT 16,$4054,[CR, LF,"BACKGROUND "]
      #ENDIF

'===============================================================================
'Now, wait (spin) until the docent's start switch is depressed.  While waiting
'mining noises will be played (sound track 2) looping automatically
'===============================================================================


       DO UNTIL (StartSwitch = Pressed ) 'LOOP UNTIL StartSwitch is pressed
         PAUSE 10
       LOOP

    Proceed:               'At this point, the start button has been pressed



    #IF (ProtoMode = 0) #THEN
         SEROUT 0,$8054,["S"]  'Fade out existing sound track.  This will quiet the mine sounds

    #ELSE
         SEROUT 16,$4054,["STOP "]
    #ENDIF


'*******************************************************************************
'All sound has been turned off. Starting the Illusion'
'*******************************************************************************


    OUTS = (INS & %1111111011111110) 'Blink light over operators head
    PAUSE 500

    IF (StartSwitch = Released)  THEN continue3     'Don't wait any longer.  Let the ride begin
    PAUSE 500
    IF (StartSwitch = Released)  THEN continue3     'Don't wait any longer.  Let the ride begin
    PAUSE 1000
    IF (StartSwitch = Pressed)  THEN GOSUB IntroFeatures   'Button has been pressed over 2 seconds
                                                           'Play the introduction
    continue3:

    OUTS = (INS | %0000001111111110) 'Turn all lights back on

'*** Initial Bell Procedure (at start)***********************************************
'The following actions take place before the simulated ride starts down.
'Sound the BELL with the 3-3-2 sequence

    index2 = 3            'Ring bell 3 times
    GOSUB BellRing
    PAUSE 500
    GOSUB BellRing        'Ring bell another 3 times
    PAUSE 500
    index2 = 2            'ring bell twice more
    GOSUB BellRing

    'Turn off the normal ambient lighting circuit
     HIGH Relay_RoomLight

RETURN    'End of StrtState:



'******** ACCELERATION STATE PROCEDURE **********************************************

'This procedure controls the progressive activation of the lights used to simulate
'the forward movement of the man skip. These lights are referred to as strobe lights
'in this program due to the relatively short time they on on. At the start, the most
'distant light, #1, is illuminated. At about 15 feet #1 light is extinguished, and #2
'light is illuminated. The progression continues at 15 foot intervals until all lamps
'are extinguished.

AccelState:


    GOSUB one                 'Display first light
    PAUSE 2000                'Wait to simulate manskip acceleration

    HIGH Relay_Vibrater       'Start the Man Skip vibrator
    GOSUB two                 'Display second light
    PAUSE 1800

    #IF (ProtoMode = 0) #THEN
         SEROUT 0,$8054,["1"]  'Illusion Sounds
    #ELSE
         SEROUT 16,$4054,["ILLUSION "]
    #ENDIF

    GOSUB three               'Display third light
    PAUSE 1400

    GOSUB four                'Display 4th light
    PAUSE 1200

    GOSUB five                'Display 5th light
    PAUSE 1060

    GOSUB six                 'Display 6th light
    PAUSE 950

    #IF (No7 = 0) #THEN
      GOSUB seven             'Turn on 7th light if No7 = 0
      PAUSE 900
    #ENDIF

    #IF (No8 = 0) #THEN
      GOSUB eight             'Turn on 8th light if No8 = 0
      PAUSE 900
    #ENDIF


    #IF (No9 = 0) #THEN
      GOSUB nine              'Turn on 9th light if No9 = 0
      PAUSE 900
    #ENDIF

    GOSUB NoLights

RETURN  'The end of the routine that accelerates the Illusion down the shaft

'************************************************************************************
'******** CONSTANT SPEED STATE PROCEDURE ******************************************
'***********************************************************************************

ConstSpeedState:

  'This procedure is similar to the AccelState procedure, but, due to the constant
  'speed the time between switching the lights is constant, thus a more compact coding
  'is feasible.
  'This routine will work for both up and down movement controlled by UpDown variable


   InterblockTime = 2000         'Delay time in milliseconds before simulating
                                        'passage through the next sequence of mine
                                        'shaft strobe lights
   InterlightTime = 625          'Time in milliseconds between switching the
                                        'strobe lights
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 439
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 291
   GOSUB ExecLights


   InterblockTime = 1000
   InterlightTime = 229
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 271
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 395
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 189
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 354
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 188
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 354
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 375
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 186
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 186
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 355
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 229
   GOSUB ExecLights

   InterblockTime = 1000
   InterlightTime = 300
   GOSUB ExecLights

   #IF (No9 = 1) #THEN    'Adding loops if lights are turned off so the time is about the same
   InterblockTime = 1500
   InterlightTime = 400
   GOSUB ExecLights
   #ENDIF

   #IF (No8 = 1) #THEN
   InterblockTime = 1500
   InterlightTime = 500
   GOSUB ExecLights
   #ENDIF

   #IF (No7 = 1) #THEN
   InterblockTime = 1500
   InterlightTime = 600
   GOSUB ExecLights
   #ENDIF

RETURN   'End of constant speed state

'************************************************************************************
ExecLights:

  PAUSE InterblockTime

  IF UpDown = 1 THEN      'Going down if UpDown was set to 1

    ' Now step through switching on and off the 9 mine shaft lights creating the
    ' stroboscopic illusion that the manskip is moving down the shaft.

      GOSUB one
      PAUSE  InterlightTime            'Wait the defined on/off time
      GOSUB Two
      PAUSE  InterlightTime
      GOSUB three
      PAUSE  InterlightTime
      GOSUB four
      PAUSE  InterlightTime
      GOSUB five
      PAUSE  InterlightTime
      GOSUB six
      PAUSE  InterlightTime
      #IF (No7 = 0) #THEN
        GOSUB seven
        PAUSE  InterlightTime
      #ENDIF

      #IF (No8 = 0) #THEN
        GOSUB eight
        PAUSE  InterlightTime
      #ENDIF

      #IF (No9 = 0) #THEN
        GOSUB nine
        PAUSE  InterlightTime
      #ENDIF
   ELSE  'Going up if UpDown was set to 0

   ' Now step through switching on and off the 9 mine shaft lights creating the
    ' stroboscopic illusion that the manskip is moving up the shaft.

      #IF (No9 = 0) #THEN
        GOSUB nine                       'Turn on light 9
        PAUSE  InterlightTime            'Wait the defined on/off time
      #ENDIF

      #IF (No8 = 0) #THEN
        GOSUB eight
        PAUSE  InterlightTime
      #ENDIF

      #IF (No7 = 0) #THEN
        GOSUB seven
        PAUSE  InterlightTime
      #ENDIF

      GOSUB Six
      PAUSE  InterlightTime

      GOSUB Five
      PAUSE  InterlightTime

      GOSUB Four
      PAUSE  InterlightTime

      GOSUB Three
      PAUSE  InterlightTime

      GOSUB Two
      PAUSE  InterlightTime

      GOSUB One
      PAUSE  InterlightTime

   ENDIF

      GOSUB NoLights

RETURN   'end of routine that flashes the lights sequencely up the mine

'************************************************************************************
'******** DECELERATION STATE PROCEDURE **********************************************

'The actions of this procedure can be considered the complement of those in the
'Acceleration State Procedure. Speed of the simulated manskip is diminished to zero;
'light #7 is left illuminate; the sound is terminated, as is the vibrator.

DecelState:
  PAUSE 500

  GOSUB One
  PAUSE 770                   'Increasing wait time simulating deceleration

  GOSUB Two
  PAUSE 820

  GOSUB Three
  PAUSE 880

  GOSUB Four
  PAUSE 950

  GOSUB Five
  PAUSE 1060

  GOSUB Six
  PAUSE 1200

  #IF (No7 = 0) #THEN
  GOSUB Seven
  #ENDIF
  PAUSE 1400

  #IF (No8 = 0) #THEN
  GOSUB eight
  #ENDIF

  LOW Relay_Vibrater          'Stop the Man Skip vibrator

  #IF (ProtoMode = 0) #THEN
       SEROUT 0,$8054,["S"]  'Fade out existing sound track.
  #ELSE
       SEROUT 16,$4054,["STOP "]
  #ENDIF

  PAUSE 1800

  GOSUB NoLights

RETURN   'End of deceleration state

'************************************************************************************
'*** STOP STATE PROCEDURE ***********************************************************
'The following actions take place after the simulated ride has come to a stop.
StpState:

  'Sound the bell and turn on Light #7 to inform the docent-operator the dynamic
  'aspect of the simulation has concluded. The docent may react with the audience
  'at this time when the noise of the simulation has subsided and the illumination
  'remains DIM.  When the stop button is again pressed all lights will turn on and
  'the background sounds will start.  Optionally,if the docent holds the button down
  'for more than 1 full second, the ride will go back up before turning the lights on.

  GOSUB Eight       'Turn on the lamp at the docent's station

  index2 = 1        'Ring bell once
  GOSUB BellRing


  'Now, wait (spin) until the docent's start switch is depressed.  If depressed
  'for more than 2 seconds the man skiff will go back up.  I check the switch
  'after 1 second and immediately quit if the switch has been released instead
  'of waiting the full 2 seconds
    Flag3 = 0                             'Use to decide if we travel back up
    continue0:
    IF (StartSwitch = Released ) THEN continue0   'Waiting for button press

    'Blink light 8
    GOSUB NoLights
    PAUSE 500
    GOSUB eight
    IF (StartSwitch = Released)  THEN continue1   'Don't wait any longer
    PAUSE 500
    IF (StartSwitch = Released)  THEN continue1   'Don't wait any longer
    PAUSE 1000
    GOSUB NoLights  'Turn light back off and start up ride
    IF (StartSwitch = Pressed)  THEN Flag3 = 1   'Button has been pressed over 2 seconds
                                          'so man skiff will go back up
    continue1:

RETURN  'End of routine that puts the Illusion in the STOP state

'************************************************************************************
UpStart:  'The ride will accelerate in the up direction


'     Sound the bell with the 3-3-1 sequence (Up with caution)

    index2 = 3            'Ring bell 3 times
    GOSUB BellRing
    PAUSE 500
    GOSUB BellRing        'Ring bell another 3 times
    PAUSE 500
    index2 = 1            'ring bell once more
    GOSUB BellRing

     GOSUB eight          'Turn on light 8.  This is ignoring the No8 flag
     PAUSE 1800

     HIGH Relay_Vibrater

     #IF (ProtoMode = 0) #THEN
         SEROUT 0,$8054,["1"] 'Illusion Sounds for ride up
     #ELSE
         SEROUT 16,$4054,["ILLUSION "]
     #ENDIF

     GOSUB seven            'This is ignoring the No7 flag at this point
     PAUSE 1400

     GOSUB six
     PAUSE 1200

     GOSUB five
     PAUSE 1060

     GOSUB four
     PAUSE 950

     GOSUB three
     PAUSE 880

     GOSUB two
     PAUSE 820

     GOSUB one
     PAUSE 770

RETURN 'End of the routine accelerating the the ride up

'**********************************************************************************
UpDecel:

     #IF (No8 = 0) #THEN    'Decelerate lights to a stop
     GOSUB eight
     PAUSE 770
     #ENDIF

     #IF (No7 = 0) #THEN
     GOSUB seven
     PAUSE 820
     #ENDIF

     GOSUB six
     PAUSE 880

     GOSUB five
     PAUSE 950

     GOSUB four
     PAUSE 1060

     GOSUB three
     PAUSE 1200

     GOSUB two
     PAUSE 1400

     #IF (ProtoMode = 0) #THEN
         SEROUT 0,$8054,["S"] 'Fade out the sound
     #ELSE
         SEROUT 16,$4054,["STOP "]
     #ENDIF

     GOSUB one
     PAUSE 800

     LOW relay_Vibrater

     index2 = 1            'ring bell
     GOSUB BellRing

RETURN   'End of the up deceleration

'**********************************************************************************
'Rings the bell on the Illusion or sounds a buzzer on the Proto Board
'**********************************************************************************
BellRing:

#IF (ProtoMode = 0) #THEN     '0 if real Illusion bd, 1 if proto board
  FOR index1 = 1 TO index2
    HIGH Bell_Sounder         'Turn on the Bell circuit
    PAUSE 375                 'Bell duration
    LOW Bell_Sounder          'Turn off the bell
    PAUSE 125
  NEXT

#ELSE                         'Uses speaker on proto board

  FOR index1 = 1 TO index2
    FREQOUT Bell_Sounder2, 375, Freq1Bell, freq2Bell
    PAUSE 125
  NEXT

#ENDIF

RETURN

'*********************************************************************************
'***********IntroFeatures introduces the Illusion features in a less scary mode **
'*********************************************************************************

IntroFeatures:

OUTS = (INS | %0000001111111110) 'Turn all lights back on

#IF (ProtoMode = 0) #THEN
      SEROUT 0,$8054,["4"] 'Play Introduction sound track
  #ELSE
      SEROUT 16,$4054,["INTRODUCTION "]
  #ENDIF

  PAUSE 25000

  UpDown = 1                 'Strobe the lights once up the shaft
  InterblockTime = 900
  InterlightTime = 900
  GOSUB ExecLights

  InterblockTime = 700      'once more faster
  InterlightTime = 700
  GOSUB ExecLights

  HIGH Relay_Vibrater

  InterblockTime = 500      'once more faster
  InterlightTime = 500
  GOSUB ExecLights

  LOW  Relay_Vibrater

  PAUSE 16000

  #IF (ProtoMode = 0) #THEN
      SEROUT 0,$8054,["S"] 'Fade out the sound
  #ELSE
      SEROUT 16,$4054,["STOP "]
  #ENDIF

  GOSUB all                  'Turn on all the lights in the shaft

  PAUSE 2000

RETURN


'**********************************************************************************
one:         'Read the 16 pin states, use the AND function to turn off all 9
             'LED's then use the OR function to turn on Light 1
#IF (ProtoMode = 0) #THEN                       '0 if real board, 1 if proto board
    OUTS = (INS & %1111110000000001) | Light_1  ' Turns on Light 1
#ELSE
    OUTS = (INS & %1111111000000001) | %010010000 '1 on the 7 segment LED display.
                                                '7 segment LED uses all lower number pins
                                                'so no need to look at higher pin states
#ENDIF
RETURN

'**********************************************************************************
two:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS & %1111110000000001) | Light_2 'Turns on light 2
#ELSE
    OUTS = (INS & %1111111000000001) | %100111010 '2 on the 7 segment LED display
#ENDIF
RETURN

'**********************************************************************************
three:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS & %1111110000000001) | Light_3 'Turns on light 3
#ELSE
    OUTS = (INS & %1111111000000001) | %110011010 '3
#ENDIF
RETURN

'**********************************************************************************
four:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS & %1111110000000001) | Light_4 'Turns on Light 4
#ELSE
    OUTS = (INS & %1111111000000001) | %010010110  '4
#ENDIF
RETURN

'**********************************************************************************
five:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS & %1111110000000001) | Light_5 'Turns on Light 5
#ELSE
    OUTS = (INS & %1111111000000001) | %110001110                           '5
#ENDIF
RETURN

'**********************************************************************************
six:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS & %1111110000000001) | Light_6 'Turns on light 6
#ELSE
    OUTS = (INS & %1111111000000001) | %110100110     '6
#ENDIF
RETURN

'**********************************************************************************
seven:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS & %1111110000000001) | Light_7 'Turns on light 7
#ELSE
    OUTS = (INS & %1111111000000001) | %010011000     '7
#ENDIF
RETURN

'**********************************************************************************
eight:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS & %1111110000000001) | Light_8 'Turns on light 8
#ELSE
    OUTS = (INS & %1111111000000001) | %110111110  '8
#ENDIF
RETURN

'**********************************************************************************
nine:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS & %1111110000000001) | Light_9 'Turns on light 9
#ELSE
   OUTS = (INS & %1111111000000001) | %010011110    '9
#ENDIF
RETURN

'**********************************************************************************
all:
#IF (ProtoMode = 0) #THEN
    OUTS = (INS | %0000001111111110)          'Turns on all 9 lights
#ELSE
   OUTS = (INS  | %0000000111111110)     'Turned on all LED segments on Proto Board
#ENDIF

RETURN

'**********************************************************************************
NoLights:
#IF ProtoMode = 0 #THEN
      OUTS = (INS & %1111110000000001)       'Turns off all lights
#ELSE
      OUTS = (INS & %1111111000000001)      'Turns off all LED segments on Proto Board
#ENDIF
RETURN
'**********************************************************************************
ServiceWait:

PAUSE 2000
 DO UNTIL (StartSwitch = Pressed ) 'LOOP UNTIL StartSwitch is pressed
 LOOP
RETURN

'**********************************************************************************
'************************************************************************************